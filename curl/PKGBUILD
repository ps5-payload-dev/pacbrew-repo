pkgname=ps5-payload-curl
pkgver=8.17.0
pkgrel=1
pkgdesc='An URL retrieval utility and library'
arch=('any')
url='https://curl.haxx.se'
license=('curl')
source=("https://curl.haxx.se/download/curl-${pkgver}.tar.xz")
sha256sums=('955f6e729ad6b3566260e8fef68620e76ba3c31acf0a18524416a185acf77992')
options=(!strip libtool staticlibs)
groups=('ps5-payload-dev')
depends=('ps5-payload-zlib' 'ps5-payload-openssl')
makedepends=('ps5-payload-sdk')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi

    cd curl-$pkgver
    sed -i 's|define USE_XATTR| |g' src/tool_xattr.h
    sed -i 's|hg.mozilla.org|hg-edge.mozilla.org|g' scripts/mk-ca-bundle.pl
    autoreconf -fi
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd curl-$pkgver

    # Fetch CA bundle
    ./scripts/mk-ca-bundle.pl

    ./configure --prefix="${PREFIX}" --host=x86_64-pc-freebsd \
		--enable-static --disable-shared \
		--with-openssl \
		--with-ca-bundle="${PREFIX}/etc/ca-bundle.crt" \
		--disable-docs
    ${MAKE}
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd curl-$pkgver

    export DESTDIR="${pkgdir}/${DESTDIR}"
    ${MAKE} install

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PREFIX}"

    mkdir -p "${DESTDIR}/${PREFIX}/etc"
    cp ca-bundle.crt "${DESTDIR}/${PREFIX}/etc"

    mkdir -p "${pkgdir}/${PS5_PAYLOAD_SDK}/bin"
    ln -s "../target/${PREFIX}/bin/curl-config" \
       "${pkgdir}/${PS5_PAYLOAD_SDK}/bin/prospero-curl-config"
}
