pkgname=ps5-payload-libass
pkgver=0.17.3
pkgrel=1
pkgdesc='A portable subtitle renderer'
arch=('any')
url="https://github.com/libass/libass"
license=('custom: ISC')
options=(!strip libtool staticlibs)
source=("https://github.com/libass/libass/releases/download/$pkgver/libass-$pkgver.tar.xz")
sha256sums=('SKIP')
depends=('ps5-payload-libfribidi' 'ps5-payload-freetype' 'ps5-payload-harfbuzz'
	'ps5-payload-fontconfig')
makedepends=('ps5-payload-sdk')
groups=('ps5-payload-dev')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi

    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd libass-$pkgver

    autoreconf -fi
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd libass-$pkgver

    export CFLAGS="-O1"
    ./configure --prefix="${PREFIX}" --host=x86_64-pc-freebsd \
		--enable-static --disable-shared \
		--disable-asm --enable-large-tiles 
    ${MAKE} V=1
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd libass-$pkgver

    export DESTDIR="${pkgdir}/${DESTDIR}"
    make install

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PREFIX}"
}
