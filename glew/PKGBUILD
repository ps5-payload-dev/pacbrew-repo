pkgname=ps5-payload-glew
pkgver=2.2.0
pkgrel=1
pkgdesc="The OpenGL Extension Wrangler Library"
arch=('any')
url="https://github.com/nigels-com/glew"
license=("LicenseRef-glew")
options=(!strip libtool staticlibs)
source=("https://github.com/nigels-com/glew/releases/download/glew-$pkgver/glew-$pkgver.tgz")
sha256sums=('d4fc82893cfb00109578d0a1a2337fb8ca335b3ceccf97b97e5cc7f08e4353e1')
groups=('ps5-payload-dev' 'ps5-payload-gl')
depends=('ps5-payload-mesa')
makedepends=('ps5-payload-sdk')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi

    cd glew-$pkgver
    sed -i 's|find_package (OpenGL REQUIRED)||g' build/cmake/CMakeLists.txt
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd glew-$pkgver

    ${CMAKE} -DCMAKE_BUILD_TYPE=Release \
	     -DGLEW_OSMESA=YES \
	     -DBUILD_UTILS=NO \
	     -B builddir \
             -S build/cmake
    ${MAKE} -C builddir
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd glew-$pkgver

    export DESTDIR="${pkgdir}/${DESTDIR}"
    ${MAKE} -C builddir install

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PREFIX}"
}
