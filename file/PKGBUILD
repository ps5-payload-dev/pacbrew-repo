pkgname=ps5-payload-file
pkgver=5.46
pkgrel=1
pkgdesc='File type identification utility'
arch=('any')
license=('custom')
url='https://www.darwinsys.com/file/'
options=(!strip libtool staticlibs)
source=("https://astron.com/pub/file/file-$pkgver.tar.gz")
sha256sums=('c9cc77c7c560c543135edc555af609d5619dbef011997e988ce40a3d75d86088')
makedepends=('ps5-payload-sdk')
depends=('ps5-payload-zlib' 'ps5-payload-xz' 'ps5-payload-bzip2')
groups=('ps5-payload-dev')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd file-$pkgver

    ./configure --prefix="${PREFIX}" --host=x86_64-pc-freebsd \
		--enable-static --disable-shared
    ${MAKE} V=1
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd file-$pkgver

    export DESTDIR="${pkgdir}/${DESTDIR}"
    ${MAKE} install

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PREFIX}"
}
