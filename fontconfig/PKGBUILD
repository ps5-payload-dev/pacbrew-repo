pkgname=ps5-payload-fontconfig
pkgver=2.15.0
pkgrel=1
pkgdesc="Library for configuring and customizing font access"
url='https://www.freedesktop.org/wiki/Software/fontconfig'
arch=('x86_64')
license=('custom')
options=(!strip libtool staticlibs)
source=("https://www.freedesktop.org/software/fontconfig/release/fontconfig-$pkgver.tar.gz")
sha256sums=('SKIP')
groups=('ps5-payload-dev')
depends=('ps5-payload-freetype' 'ps5-payload-expat')
makedepends=('ps5-payload-sdk')

prepare() {
    export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd fontconfig-$pkgver

    ./configure --prefix="${PREFIX}" --host=x86_64-pc-freebsd \
		--enable-static --disable-shared \
		--with-default-fonts="/preinst/common/font,/system_ex/common_ex/font2,${PREFIX}/share/fonts"
    ${MAKE}
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd fontconfig-$pkgver

    export DESTDIR="${pkgdir}/${DESTDIR}"
    make install

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PREFIX}"
}
