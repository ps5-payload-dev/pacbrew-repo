pkgname=ps5-payload-libnfs
pkgver=5.0.3
pkgrel=1
pkgdesc="client library for accessing NFS shares"
url="https://github.com/sahlberg/libnfs"
arch=('any')
license=('GPL')
options=(!strip libtool staticlibs)
source=("https://github.com/sahlberg/libnfs/archive/libnfs-$pkgver.tar.gz")
sha256sums=('d945cb4f4c8f82ee1f3640893a168810f794a28e1010bb007ec5add345e9df3e')
makedepends=('ps5-payload-sdk')
groups=('ps5-payload-dev')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi

    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd libnfs-libnfs-$pkgver

    autoreconf -vif
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd libnfs-libnfs-$pkgver

    export CFLAGS="-Wno-cast-align"
    ./configure --prefix="${PREFIX}" --host=x86_64-pc-freebsd \
		--enable-static --disable-shared \
		--without-libkrb5
    ${MAKE}
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd libnfs-libnfs-$pkgver

    export DESTDIR="${pkgdir}/${DESTDIR}"
    make install

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PREFIX}"
}

