pkgname=ps5-payload-xz
pkgver=5.4.6
pkgrel=1
pkgdesc='Library and command line tools for XZ and LZMA compressed files'
arch=('any')
url='https://github.com/tukaani-project/xz'
license=('GPL' 'LGPL' 'custom')
options=(!strip libtool staticlibs)
source=("https://github.com/tukaani-project/xz/releases/download/v${pkgver}/xz-${pkgver}.tar.xz")
sha256sums=('b92d4e3a438affcf13362a1305cd9d94ed47ddda22e456a42791e630a5644f5c')
groups=('ps5-payload-dev')
makedepends=('ps5-payload-sdk')


prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi

    cd xz-$pkgver
    ./autogen.sh
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd xz-$pkgver

    ./configure --prefix="${PREFIX}" --host=x86_64-pc-freebsd \
		--enable-static --disable-shared \
		--disable-nls --disable-rpath \
		--disable-scripts
    ${MAKE}
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd xz-$pkgver

    export DESTDIR="${pkgdir}/${DESTDIR}"
    make install

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PREFIX}"
}
