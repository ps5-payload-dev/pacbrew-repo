pkgname=ps5-payload-sdl2
pkgver=2.30.2.git
pkgrel=1
pkgdesc='A library for portable low-level access to video, audio, joysticks...'
arch=('x86_64')
url='https://libsdl.org'
license=('LGPL')
options=(!strip libtool staticlibs)
source=('git+https://github.com/ps5-payload-dev/SDL.git')
sha256sums=('SKIP')
groups=('ps5-payload-dev' 'ps5-payload-sdl2')
depends=('ps5-payload-libsamplerate')
makedepends=('ps5-payload-sdk')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd SDL

    ${CMAKE} -DCMAKE_BUILD_TYPE=Release \
	     -B build \
             -S .
    ${MAKE} -C build
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd SDL

    export DESTDIR="${pkgdir}/${DESTDIR}"
    ${MAKE} -C build install

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PREFIX}"

    mkdir -p "${pkgdir}/${PS5_PAYLOAD_SDK}/bin"
    ln -s "../target/${PREFIX}/bin/sdl2-config" \
       "${pkgdir}/${PS5_PAYLOAD_SDK}/bin/prospero-sdl2-config"
}
