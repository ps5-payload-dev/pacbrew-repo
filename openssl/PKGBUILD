pkgname=ps5-payload-openssl
pkgver=3.5.2
pkgrel=1
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security'
url='https://www.openssl.org'
license=('Apache-2.0')
arch=('any')
options=(!strip libtool staticlibs)
source=("https://github.com/openssl/openssl/releases/download/openssl-${pkgver}/openssl-${pkgver}.tar.gz")
sha256sums=('c53a47e5e441c930c3928cf7bf6fb00e5d129b630e0aa873b08258656e7345ec')

groups=('ps5-payload-dev')
makedepends=('ps5-payload-sdk')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd openssl-$pkgver

    ./Configure BSD-x86_64 no-tests no-apps no-shared --prefix="${PREFIX}"
    ${MAKE} build_sw
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd openssl-$pkgver

    export DESTDIR="${pkgdir}/${DESTDIR}"
    ${MAKE} install_sw DESTDIR=$DESTDIR

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PREFIX}"
}
