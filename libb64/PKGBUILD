pkgname=ps5-payload-libb64
pkgver=1.2.1
pkgrel=1
pkgdesc='Base64 Encoding/Decoding Routines'
url='http://libb64.sourceforge.net/'
arch=('any')
license=('custom:Public Domain')
options=(!strip libtool staticlibs)
source=("https://downloads.sourceforge.net/libb64/libb64-${pkgver}.zip"
        "https://sources.debian.net/data/main/libb/libb64/1.2-3/debian/patches/bufsiz-as-buffer-size.diff")
sha256sums=('20106f0ba95cfd9c35a13c71206643e3fb3e46512df3e2efb2fdbf87116314b2'
            '0a8a978e94a924b4f62795de251aa1a5ceb53a1024ea60193d7f3e5a5794fa20')
groups=('ps5-payload-dev')
makedepends=('ps5-payload-sdk')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi
    cd libb64-$pkgver
    patch -p1 -i "$srcdir"/bufsiz-as-buffer-size.diff
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd libb64-${pkgver}/src
    ${MAKE}
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd libb64-${pkgver}

    install -Dm 644 src/libb64.a -t "${pkgdir}/${DESTDIR}/${PREFIX}/lib"
    cp -r include "${pkgdir}/${DESTDIR}/${PREFIX}/"
}
